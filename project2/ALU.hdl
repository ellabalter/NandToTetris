// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
  //zx
    Mux16(a=x, b=false, sel=zx, out=x1);
  //nx
    Not16(in=x1, out=Notx);
    Mux16(a=x1, b=Notx, sel=nx, out=x2);
  //zy
    Mux16(a=y, b=false, sel=zy, out=y1);
  //ny
    Not16(in=y1, out=Noty1);
    Mux16(a=y1, b=Noty1, sel=ny, out=y2);
  //f
    Add16(a=x2, b=y2, out=x2Addy2);
    And16(a=x2, b=y2, out=x2Andy2);
    Mux16(a=x2Andy2, b=x2Addy2, sel=f, out=xy);
  //no
    Not16(in=xy, out=Notxy);
    Mux16(a=xy, b=Notxy, sel=no, out=out); 
  //ng 
    Mux16(a=xy, b=Notxy, sel=no, out[15]=out3);
    Or(a=false, b=out3, out=ng); 
  //zr
    Mux16(a=xy, b=Notxy, sel=no, out[0..7]=out1, out[8..15]=out2);
    Or8Way(in=out1, out=zr1);
    Or8Way(in=out2, out=zr2);
    Or(a=zr1, b=zr2, out=Notzr);
    Not(in=Notzr, out=zr);






   
}